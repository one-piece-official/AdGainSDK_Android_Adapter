plugins {
    id 'com.android.library'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                def fileName = "adgain_tobid_adapter_${extractVersionFromAar()}.aar"
                outputFileName = fileName
            }
        }
    }
}

dependencies {

    compileOnly(name: "adgain-sdk-4.2.1.2", ext: 'aar')

    // WindMill SDK dependencies
    compileOnly(name: 'windmill-sdk-4.6.20', ext: 'aar')
    compileOnly(name: 'wind-common-1.8.3', ext: 'aar')
    compileOnly(name: 'wind-sdk-4.23.2', ext: 'aar')

    compileOnly "com.github.bumptech.glide:glide:4.9.0"
}


def extractVersionFromAar() { // 获取AdGain SDK 版本号
    def dir = getParent().projectDir.path + "/tobid/libs/AdNetworks/adgain"
    def aarFiles = fileTree(dir: dir, include: 'adgain-sdk-*.aar')
    if (aarFiles.isEmpty()) {
        return null
    }
    def aarFile = aarFiles.first()
    def matcher = (aarFile.name =~ /adgain-sdk-(\d+\.\d+\.\d+\.\d)\.aar/)
    if (matcher.find()) {
        return matcher.group(1) // 返回版本号 4.2.1
    }
    def matcher1 = (aarFile.name =~ /adgain-sdk-(\d+\.\d+\.\d+)\.aar/)
    if (matcher1.find()) {
        return matcher1.group(1) // 返回版本号 4.2.1
    }
    return null
}

repositories {
    flatDir {
        dirs '../tobid/libs', '../tobid/libs/AdNetworks/adgain'
    }
}
